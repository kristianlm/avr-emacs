( morse input, enter text with 3 buttons )

( physical buttons )
: btn.A  ] portb [ $ 04 l ] ; ( MISO must be output for SPI)
: btn.A' ] portb [ $ 02 l ] ; ( PCINT2 )

: btn.X  ] portc [ $ 02 l ] ; ( PCINT10)
: btn.X' ] portc [ $ 00 l ] ;

: btn.Y  ] portc [ $ 05 l ] ; ( PCINT13)
: btn.Y' ] portc [ $ 03 l ] ;

( construct btn mask 0000 0AXY)
: btns ( -- nibble ) ] btn.A ] pin@
    [ $ 1 l ] lshift ] btn.X ] pin@ ] or
    [ $ 1 l ] lshift ] btn.Y ] pin@ ] or
    [ $ 0007 l ] xor
    ] ;

2 var btns-last      btns btns-last !
2 var btnsq          0    btnsq !

: btns.changed ( -- x ) ] btns-last ] @ ] btns ] dup ] btns-last ] ! ] xor ] ;

( button press queue into nice patterns in btnsq )
( free, press A, release A => 040 )
( free, press Y, release A => 010 )
( hold A, press Y, release Y => 454 )
: btns.q ( nibble -- ) [ $ 000f l ] and ] btnsq ] @ [ $ 4 l ] lshift ] or ] btnsq ] ! ] ;

( current morse sequence )
2 var morsing        1    morsing !
( needs length somehow: always start morsebytes with 1 )
: /morsing [ $ 1 l ] morsing ] ! ] ;

: dit  ( m -- m' ) [ $ 1 l ] lshift [ $ 0 l ] or ] ;
: dash ( m -- m' ) [ $ 1 l ] lshift [ $ 1 l ] or ] ;

(                            .|.|.|.|.|.|.|    )
(                    .|.|.|.|..||..||..||..    )
(                .|.|..||..||....||||....||    )
(              .|..||....||||........||||||    )
ihere 2 + ."   etianmsurwdkgohvf_l_pjbxcyzq__" con morsetable
( morsetable itype )

: even? [ $ 1 l ] and ] 0? ] drop ] ;
: cpick ( x a -- x' ) ] even? ] if ] high ] ; ] then ] low ] ;

: morse@ ( ma -- c ) ] dup
    ] 2/ ] morsetable ] + ] i@
    ] swap ] cpick
    ] ;

( ========== line buffer ========== )

$ 02 var at     0 at !
$ 0100 con linesize
linesize var &line    &line linesize 0 fill
( &line linesize dump )
: at! ( c --) ] at ] c@ ] &line ] + ] c! ] ; ( save c into at )
: at@ ( -- c) ] at ] c@ ] &line ] + ] c@ ] ; ( get c in line from at )
: at++ ] at ] c@ ] 1+   ] linesize ] 1- ] and   ] at ] c! ] ;
: at-- ] at ] c@ ] 1-   ] linesize ] 1- ] and   ] at ] c! ] ;
( store char c into line and move cursor )
: commit ( c -- ) ] at! ] at++ ] ;
: lineclear ( -- ) ] &line ] linesize [ $ 20 l ] fill [ $ 0 l ] at ] ! ] ;

( holder for "callback" )
2 var 'btns.handler ' drop  'btns.handler !
2 var 'btns.updater ' line. 'btns.updater !
2 var 'btns.special ' h.    'btns.special !

: update ] 'btns.updater ] @ ] execute ] ;
: special ] 'btns.special ] @ ] execute ] ;

( btnsq -- )
: btns.handler [ $ 0fff l ] and
    [ $ 0010 l ] ?? ] drop ] =if ] morsing ] @ ]   dash ] morsing ] !        ] update ] then
    [ $ 0040 l ] ?? ] drop ] =if ] morsing ] @ ]    dit ] morsing ] !        ] update ] then
    [ $ 0020 l ] ?? ] drop ] =if ] morsing ] @ ] morse@ ] commit  ] /morsing ] update ] then
    [ $ 0454 l ] ?? ] drop ] =if ] at++                           ] /morsing ] update ] then
    [ $ 0151 l ] ?? ] drop ] =if ] at--                           ] /morsing ] update ] then
    [ $ 0262 l ] ?? ] drop ] =if ] morsing ] @ ] morse@ ] special ] /morsing ] update ] then
    [ $ 0464 l ] ?? ] drop ] =if ] morsing ] @ ] morse@ ] special ] /morsing ] update ] then
    [ $ 0462 l ] ?? ] drop ] =if ] morsing ] @ ] morse@ ] special ] /morsing ] update ] then
    [ $ 0264 l ] ?? ] drop ] =if ] morsing ] @ ] morse@ ] special ] /morsing ] update ] then
    [ $ 0131 l ] ?? ] drop ] =if                                  ] /morsing ] update ] then
    ] drop
  ] ;
' btns.handler 'btns.handler !

: invert ] invert ] ;

( print char at@ but in inverted colors )
: /at@. ] at@ ] c>gi ] glyph@ ] invert ] swap ] invert ] swap ] glyph>oled ] ;

( redraw line on display )
: line.
    ] &line ] oled<row ( draw line )
    ] at ] @ ] 2* ] 2* ] oled.x ] /at@. ( draw cursor overlay )
   ] ;
' line. 'btns.updater !

( enqueue new button state if something changed )
: btns.maybeq ( -- )
    ] btns.changed ] 0? ] drop ] if
        ] btns ] btns.q
        ] btnsq ] @ ] 'btns.handler ] @ ] execute ] ;
    ] then ] ;

: btns-watch ] slow ] btns.maybeq ] btns-watch ] ;

( allocate RAM for our main task because )
( we want the forth interpreter running too )
$ 80 var &btns-watch

: morse.init
    ( init buttons )
    ." initializing morse"
    ] btn.A ] pull-up ] btn.A' ] ground
    ] btn.X ] pull-up ] btn.X' ] ground
    ] btn.Y ] pull-up ] btn.Y' ] ground
    ] lineclear
    [ ' btns.handler l ] 'btns.handler ] !
    [ ' line. l ] 'btns.updater ] !
    [ ' btns-watch l [ $ 20 l [ $ 20 l ] &btns-watch ] task-init
    ] line.
    ] ;


: morse.start ] &btns-watch ] task-queue ] ;
: morse.stop  ] &btns-watch ] task-dequeue ] ;

